#Область ПрограммныйИнтерфейс

// Регламентное перепроведение документов по расписанию с последующей отправкой отчета на электронную почту.
Процедура ГП_ПерепроведениеДокументовПоРасписанию() Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	// Получаем параметры из регистра сведений
	ПараметрыИзРегистра = РегистрыСведений.ГП_ПараметрыРегламентныхЗаданий.Получить();
	
	// Дата начала перепроведения
	ДатаНачала = ПараметрыИзРегистра.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = НачалоМесяца(ТекущаяДата);
	КонецЕсли;
	ДатаОкончания = КонецДня(ТекущаяДата);
	
	// Организации из регистра или все
	Если ЗначениеЗаполнено(ПараметрыИзРегистра.ВыбранныеОрганизации) Тогда
		ВыбранныеОрганизации = ПараметрыИзРегистра.ВыбранныеОрганизации.Получить();
		Если ТипЗнч(ВыбранныеОрганизации) <> Тип("Массив") Тогда
			ВыбранныеОрганизации = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	// Выбранные типы документов из регистра
	ВыбранныеТипы = "";
	Если ЗначениеЗаполнено(ПараметрыИзРегистра.ВыбранныеТипыСтрокой) Тогда
		ВыбранныеТипы = ПараметрыИзРегистра.ВыбранныеТипыСтрокой;
	КонецЕсли;
	
	ПараметрыПерепроведения = Новый Структура;
	ПараметрыПерепроведения.Вставить("Организация", ВыбранныеОрганизации);
	ПараметрыПерепроведения.Вставить("ДатаНачала", ДатаНачала);
	ПараметрыПерепроведения.Вставить("ДатаОкончания", ДатаОкончания);
	ПараметрыПерепроведения.Вставить("ЗаписыватьОшибкиВЖурналРегистрации", Истина);
	ПараметрыПерепроведения.Вставить("ОстанавливатьсяПоОшибке", Ложь);
	ПараметрыПерепроведения.Вставить("СообщатьПрогрессВыполнения", Ложь);
	ПараметрыПерепроведения.Вставить("АдресХранилищаСОшибками", ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор));
	ПараметрыПерепроведения.Вставить("ДанныеЗаблокированы", Истина);
	ПараметрыПерепроведения.Вставить("ТолькоВыбранныеОрганизации", Истина);
	ПараметрыПерепроведения.Вставить("ВыбранныеТипы", ВыбранныеТипы); 
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
    Обработки.ГрупповоеПерепроведениеДокументов.ПерепроведениеДокументов(ПараметрыПерепроведения, АдресХранилища);
	
	// обновляем дату в регистре
	Запись = РегистрыСведений.ГП_ПараметрыРегламентныхЗаданий.СоздатьМенеджерЗаписи();
	Запись.Дата = ТекущаяДатаСеанса();
	Запись.Записать();

	// письмо
	Получатели = ПолучателиПисьма();
	Если Получатели = "" Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла("txt");
	
    Попытка     
        РезультатыПерепроведения = ПолучитьИзВременногоХранилища(АдресХранилища);
        ТабДок = ПолучитьИзВременногоХранилища(РезультатыПерепроведения.АдресХранилищаСОшибками).ОтчетПоОшибкам;
        ТабДок.Записать(ПутьКФайлу, ТипФайлаТабличногоДокумента.TXT);
    Исключение
        Текст = Новый ЗаписьТекста(ПутьКФайлу, КодировкаТекста.ANSI);
        Текст.ЗаписатьСтроку("" + ВыбранныеОрганизации);
        Текст.ЗаписатьСтроку("Выполнено перепроведение документов за период: " + ДатаНачала + " - " + ДатаОкончания);
        Текст.ЗаписатьСтроку("Ошибок при перепроведение не было.");
        Текст.Закрыть(); 
	КонецПопытки;

	ТемаСообщения = "Отчет о проведении документов за " + Формат(ТекущаяДата, "ДФ=dd.MM.yyyy");
	Тело = ТемаСообщения;

	ОписаниеВложения = Новый Структура;
	ОписаниеВложения.Вставить("Представление", ТемаСообщения + ".txt");
	ОписаниеВложения.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу)));

	ПараметрыСообщения = Новый Структура;	
	ПараметрыСообщения.Вставить("Кому",     Получатели);
	ПараметрыСообщения.Вставить("Тема",     ТемаСообщения);
	ПараметрыСообщения.Вставить("Тело",     Тело);
	ПараметрыСообщения.Вставить("Вложения", Новый Массив);
	
	ПараметрыСообщения.Вложения.Добавить(ОписаниеВложения);
	УдалитьФайлы(ПутьКФайлу);

	Попытка
		НовоеПисьмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты, ПараметрыСообщения);
		РаботаСПочтовымиСообщениями.ОтправитьПисьмо(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты, НовоеПисьмо)
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("Ошибка отправки отчета о проведении документов", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// Обновляет параметры регламентных заданий на основании данных документа проведения.
//
// Параметры:
//   Источник - ДокументОбъект - Документ, на основании которого обновляются параметры
//   Отказ - Булево - Признак отказа от проведения документа
//   РежимПроведения - РежимПроведенияДокумента - Режим проведения документа
Процедура ОбновитьПараметрыРегламентныхЗаданий_ГПОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ГП_ПараметрыРегламентныхЗаданий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

	Попытка
		Блокировка.Заблокировать();

		Запись = РегистрыСведений.ГП_ПараметрыРегламентныхЗаданий.СоздатьМенеджерЗаписи();
		Запись.Прочитать();

		Если Запись.Выбран() Тогда
			Если Запись.Дата > Источник.Дата Тогда
				Запись.Дата = Источник.Дата;
				Запись.Документ = Источник.Ссылка;
				Запись.Записать();
			КонецЕсли;
		Иначе
			Запись.Дата = Источник.Дата;
			Запись.Документ = Источник.Ссылка;
			Запись.Записать();
		КонецЕсли;
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("Ошибка отправки отчета о проведении документов", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Инициализирует дополнительные свойства для работы расширения.
// Вызывается один раз при первом запуске системы.
//
Процедура ИнициализироватьДополнительныеСвойства() Экспорт
	
	// Проверяем, существует ли уже свойство
	ИмяСвойства = "ПолучательРассылкиОПроведении_a4469ddd895b48fc96f2b4de7ee0e443";
	СуществующееСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяСвойства);
	
	Если ЗначениеЗаполнено(СуществующееСвойство) Тогда
		// Свойство уже существует
		Возврат;
	КонецЕсли;
	
	Попытка
		// Параметры для создания дополнительного сведения
		Параметры = Новый Структура;
		Параметры.Вставить("Имя", ИмяСвойства); // Техническое имя для поиска
		Параметры.Вставить("Наименование", "Получатель рассылки о проведении"); // Отображаемое название
		Параметры.Вставить("Тип", Новый ОписаниеТипов("Булево"));
		
		// Создаем дополнительное сведение для справочника Пользователи
		УправлениеСвойствами.ДобавитьСвойство("Справочник.Пользователи", Параметры, Истина);
		
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("Инициализация дополнительных свойств", 
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Возвращает ограничение типа с только проводимыми документами.
//
// Возвращаемое значение:
//   ОписаниеТипов - описание типов, содержащее только проводимые документы
//
Функция ПолучитьОграничениеТипаПроводимыхДокументов() Экспорт
	
	МассивПроводимыхДокументов = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из Метаданные.Документы Цикл
		// Проверяем, что документ проводится
		Если ОбъектМетаданных.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
			МассивПроводимыхДокументов.Добавить(Тип("ДокументСсылка." + ОбъектМетаданных.Имя));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ОписаниеТипов(МассивПроводимыхДокументов);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучателиПисьма()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|		КонтактнаяИнформация.Представление КАК Представление
	|	ИЗ
	|		РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|			ПО ДополнительныеСведения.Объект = КонтактнаяИнформация.Объект
	|				И (КонтактнаяИнформация.Тип = Значение(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|				И (КонтактнаяИнформация.Вид = Значение(Справочник.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя))
	|	ГДЕ
	|		ДополнительныеСведения.Свойство = &Свойство
	|		И ДополнительныеСведения.Значение = Истина
	|";	

	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПолучательРассылкиОПроведении_a4469ddd895b48fc96f2b4de7ee0e443"));
	Результат = Запрос.Выполнить();
	
	Получатели = Новый Массив;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Получатели.Добавить(СокрЛП(Выборка.Представление));
	КонецЦикла;
	
	Возврат СтрСоединить(Получатели, ";");

КонецФункции

#КонецОбласти