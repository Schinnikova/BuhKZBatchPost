# Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.НовыйПостроительФорм(ЭтотОбъект)
	
	.РеквизитСтрока("ВыбранныеТипыСтрокой")
	
	.ДобавитьРеквизит("ВыбранныеТипы", Новый ОписаниеТипов("ОписаниеТипов"))
	
	.ПолеВвода("ВыбранныеТипы")
	.Заголовок("Допустимые типы")
	.ПутьКДанным("ВыбранныеТипы")
	.ОграничениеТипа(ГП_ОбщийМодуль.ПолучитьОграничениеТипаПроводимыхДокументов())
	.Подсказка("Выберите типы значений для обработки")
	.Обработчик("ПриИзменении", "ГП_ВыбранныеТипыПриИзменении")
	
	.Команда("ВыполнитьСФильтром", "ГП_ВыполнитьСФильтром")
	.Кнопка("ВыполнитьСФильтром")
	.Заголовок("Выполнить с фильтром")
	.Подсказка("Выполнить проведение с применением выбранных типов")
	.ИмяКоманды("ВыполнитьСФильтром")
	
	.Применить();
	
КонецПроцедуры

&НаСервере
Процедура ГП_ПриЗагрузкеДанныхИзНастроекНаСервереПосле(Настройки)
	
	НастройкаТипов = Настройки.Получить("ВыбранныеТипыСтрокой");
	
	Если НастройкаТипов <> Неопределено Тогда
		
		ВыбранныеТипыДляОтбора = СтрРазделить(НастройкаТипов, ";");
		МассивТипов = Новый Массив;

		Для Каждого ИмяТипа Из ВыбранныеТипыДляОтбора Цикл
			ОбъектМетаданных = Метаданные.Документы.Найти(ИмяТипа);
			Если ОбъектМетаданных <> Неопределено Тогда
				МассивТипов.Добавить(Тип("ДокументСсылка." + ИмяТипа));
			КонецЕсли;
		КонецЦикла;
	
		ЭтотОбъект.ВыбранныеТипы = Новый ОписаниеТипов(МассивТипов);

	КонецЕсли;

КонецПроцедуры             

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ГП_ВыбранныеТипыПриИзменении(Элемент)
	
	ГП_ВыбранныеТипыСтрокойНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ГП_ВыполнитьСФильтром(Команда)
	
	ГП_ВыполнитьНаСервереСФильтром();
	
КонецПроцедуры

&НаСервере
Процедура ГП_ВыбранныеТипыСтрокойНаСервере()
	
	ВыбранныеТипыДляОтбора = Новый Массив;
	Для Каждого Тип Из ЭтотОбъект.ВыбранныеТипы.Типы() Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		Если Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
			ВыбранныеТипыДляОтбора.Добавить(ОбъектМетаданных.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ЭтотОбъект.ВыбранныеТипыСтрокой = СтрСоединить(ВыбранныеТипыДляОтбора, ";");
	
КонецПроцедуры

&НаСервере
Функция ГП_ВыполнитьНаСервереСФильтром()

	Если ТолькоВыбранныеОрганизации = 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации";
		Выборка = Запрос.Выполнить().Выбрать();
		ВыбранныеОрганизации = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ВыбранныеОрганизации.Добавить(Выборка.Организация);
		КонецЦикла;
	Иначе
		ВыбранныеОрганизации = Новый Массив;
		Для Каждого ЭлементСпискаОрганизаций Из ТаблицаОрганизаций Цикл
			Если ЗначениеЗаполнено(ЭлементСпискаОрганизаций.Организация) Тогда
				ВыбранныеОрганизации.Добавить(ЭлементСпискаОрганизаций.Организация);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 

	// Возможно, что фоновое задание было запущено раньше, 
	// пользователь дал команду его отменить, однако задание не отменено.
	// В таком случае не следует запускать задание повторно - следует дождаться его выполнения.
	// Мы можем отследить ситуацию, только если все это происходит в одной форме.
	Если ОписаниеФоновогоЗадания <> Неопределено Тогда

		Если ЗаданиеЕщеВыполняется(ОписаниеФоновогоЗадания.ИдентификаторЗадания) Тогда
			Возврат Неопределено; // надо ждать
		КонецЕсли;
		ОписаниеФоновогоЗадания = Неопределено;

	КонецЕсли;

	СтруктураПараметров = Новый Структура("Организация, ДатаНачала, ДатаОкончания, ЗаписыватьОшибкиВЖурналРегистрации, ОстанавливатьсяПоОшибке, 
	|СообщатьПрогрессВыполнения, АдресХранилищаСОшибками, ДанныеЗаблокированы, ТолькоВыбранныеОрганизации, ВыбранныеТипы",
	ВыбранныеОрганизации,
	?(ЗначениеЗаполнено(Объект.ДатаНачала), Объект.ДатаНачала, Неопределено),
	?(ЗначениеЗаполнено(Объект.ДатаОкончания), Объект.ДатаОкончания, Неопределено),
	Истина,
	Объект.ОстанавливатьсяПоОшибке,
	Истина,
	"",
	Истина, // фильтр по метаданным
 	?(ТолькоВыбранныеОрганизации = 0, Ложь, Истина),
	ЭтотОбъект.ВыбранныеТипыСтрокой);

	// Временное хранилище для отчета об ошибках необходимо создавать
	// в том же сеансе работы с пользователем, из которого требуется обращение к ним, 
	// т.к. временное хранилище очищается при завершении сеанса фонового задания.
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров.АдресХранилищаСОшибками) Тогда
		СтруктураПараметров.Вставить("АдресХранилищаСОшибками", ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор));
	КонецЕсли;

	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);

	КлючЗадания = "";	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Групповое перепроведение документов'");
	ПараметрыВыполнения.КлючФоновогоЗадания = КлючЗадания;
	// Если на файловой базе уже запущено любое другое фоновое задание, то новое встанет в очередь, а не будет запущено в основном потоке.
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ОписаниеФоновогоЗадания = ДлительныеОперации.ВыполнитьПроцедуру(
	ПараметрыВыполнения,
	"Обработки.ГрупповоеПерепроведениеДокументов.ПерепроведениеДокументов",
	СтруктураПараметров,
	АдресХранилища);

	ИдентификаторЗадания = ОписаниеФоновогоЗадания.ИдентификаторЗадания;

	Результат = ОбщегоНазначения.СкопироватьРекурсивно(ОписаниеФоновогоЗадания);

	Результат.Вставить("ВыбранныеОрганизации",        ВыбранныеОрганизации);
	Результат.Вставить("ДатаНачалаПерепроведения",    Объект.ДатаНачала);
	Результат.Вставить("ДатаОкончанияПерепроведения", Объект.ДатаОкончания);

	Возврат Результат;

КонецФункции

#КонецОбласти

