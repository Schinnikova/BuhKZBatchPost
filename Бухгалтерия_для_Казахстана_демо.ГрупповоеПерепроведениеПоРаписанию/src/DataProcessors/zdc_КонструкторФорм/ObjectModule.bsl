////////////////////////////////////////////////////////////////////////////////
// Конструктор создания форм
// https://infostart.ru/1c/2303678/
// https://github.com/QZeddicus/Fluent-Form-Constructor-1C
// ©️ Алексей Сапрунов aka Zeddicus
// 
// Методы начинающиеся на "Новый" и "Получить" возвращают конечные данные
// и не могут использвоваться для продолжения Текучего строителя
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Стандартные реквизиты формы
Перем Форма;
Перем Элементы;
Перем Команды;
Перем Параметры;
Перем УсловноеОформление;
// Конец Стандартные реквизиты формы

Перем Интерфейс85;               // Булево - Режим интерфейса версии 8.5
Перем ТекущийЭлемент;            // ЭлементФормы - Последний созданный элемент
Перем КешЭлементов;              // Соответствие - Кеш всех созданных элементов
Перем ТекущаяКоманда;            // КомандаФормы - Последняя соданная команда
Перем КешКоманд;                 // Соответствие - Кеш всех созданных команд
Перем УсловноеОформлениеЭлемент; // Элемент условного оформления - Последний созданный элемент
Перем УсловноеОформлениеКеш;     // Соответствие - Кеш всех созданных элементов условного оформления

Перем РеквизитыФормы;            // Соответствие - Реквизиты формы
Перем СозданныеРеквизиты;        // Соответствие - Пути к созданным реквизитам
Перем ОсновнойРеквизитОписание;  // Описание основного реквизита формы - Имя и сам реквизит
Перем ДобавляемыеРеквизиты;      // Массив - Добавляемые реквизиты
Перем УдаляемыеРеквизиты;        // Массив - Удаляемые реквизиты

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область ТекучиеМетоды

// Выполняется инициализация конструктора - кэшируется данные формы
//
// Параметры:
//  ДорабатываемаяФорма - ФормаКлиентскогоПриложения - Форма клиентского приложения
//  Режим85 - Булево - Указывается, если дорабатывается форма для интерфейса версии 8.5
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция Инициализировать(ДорабатываемаяФорма, Режим85 = Ложь) Экспорт
	
	Форма = ДорабатываемаяФорма;
	Элементы = Форма.Элементы;
	Команды = Форма.Команды;
	Параметры = Форма.Параметры;
	УсловноеОформление = Форма.УсловноеОформление;
	
	Интерфейс85 = Режим85;
	
	ЭтоПервый = Истина;
	Реквизиты = Форма.ПолучитьРеквизиты();
	Для Каждого ТекРеквизит Из Реквизиты Цикл
		Имя = ТекРеквизит.Имя;
		РеквизитыФормы.Вставить(Имя, ТекРеквизит);
		Если НЕ ЭтоПервый Тогда
			Продолжить;
		КонецЕсли;
		ЭтоПервый = Ложь;
		Если (СтрНайти(Имя, "Объект") > 0 ИЛИ СтрНайти(Имя, "Список") > 0 ) Тогда
			ОсновнойРеквизитОписание.Имя = Имя;
			ОсновнойРеквизитОписание.Реквизит = ТекРеквизит;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЭтотОбъект;
	
КонецФункции

#Область РаботаСРеквизитами

// Создаёт реквизит, если его ещё не существовало, и добавляет в массив ДобавляемыеРеквизиты.
//
// Параметры:
//  Имя - Строка - имя реквизита формы. 
//  ТипРеквизита - Строка, Массив(Тип), ОписаниеТипов - описание типа реквизита.
//                 Параметр будет передан в конструктор объекта ОписаниеТипов.
//  Путь - Строка - Путь к создаваемому реквизиту ИСКЛЮЧАЯ имя реквизита. 
//  Заголовок - Строка - заголовок. Если не указан, то будет использовано имя.
//  СохраняемыеДанные - Булево - Реквизит содержит сохраняемые данные.
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьРеквизит(Имя, ТипРеквизита, Путь = "", Заголовок = "", СохраняемыеДанные = Ложь) Экспорт
	
	Если ЕстьРеквизитИлиСвойствоОбъекта(Форма, Имя) Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	Если ТипЗнч(ТипРеквизита) = Тип("Тип") Тогда
		ТипРеквизита = ЗначениеВМассиве(ТипРеквизита);
	КонецЕсли;
	
	ОписаниеТиповРеквизита = Новый ОписаниеТипов(ТипРеквизита);
	Реквизит = Новый РеквизитФормы(Имя, ОписаниеТиповРеквизита, Путь, Заголовок, СохраняемыеДанные);
	ДобавляемыеРеквизиты.Добавить(Реквизит);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Создаёт реквизит формы, если его ещё не существовало, и добавляет в ДобавляемыеРеквизиты.
//
// Параметры:
//  Путь - Строка - Путь к создаваемому реквизиту ВКЛЮЧАЯ имя реквизита. 
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция УдалитьРеквизит(Путь = "") Экспорт
	
	Если СозданныеРеквизиты.Получить(Путь) = Неопределено Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	УдаляемыеРеквизиты.Добавить(Путь);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Изменяет реквизиты формы (добавление/удаление ранее созданных реквизитов)
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция ИзменитьРеквизиты() Экспорт
	
	Если ДобавляемыеРеквизиты.Количество() = 0 И УдаляемыеРеквизиты.Количество() = 0 Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для Каждого ТекРеквизит Из ДобавляемыеРеквизиты Цикл
		ПолныйПуть = СтрШаблон("%1.%2", ТекРеквизит.Путь, ТекРеквизит.Имя);
		СозданныеРеквизиты.Вставить(ПолныйПуть, ТекРеквизит.Имя);
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеРеквизиты Цикл
		Если СозданныеРеквизиты.Получить(Элемент) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СозданныеРеквизиты.Удалить(Элемент);
	КонецЦикла;
	
	ДобавляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты = Новый Массив;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Процедура добавляет реквизит в параметры запрета редактирования
//
// Параметры:
//  ИмяРеквизита - Строка - Имя блокируемого реквизита
//  БлокируемыеЭлементы - Строка - Один или несколько элементов формы через запятую. Элементы, которые будут заблокированы
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция ДобавитьРеквизитВПараметрыЗапретаРедактирования(ИмяРеквизита, БлокируемыеЭлементы) Экспорт
	
	Если РеквизитыФормы["ПараметрыЗапретаРедактированияРеквизитов"] = Неопределено Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	РедактированиеРазрешено = НЕ ЗначениеЗаполнено(Параметры.Ключ);
	
	ОписаниеРеквизита = Форма["ПараметрыЗапретаРедактированияРеквизитов"].Добавить();
	ОписаниеРеквизита.ИмяРеквизита = ИмяРеквизита;
	ОписаниеРеквизита.ПравоРедактирования = Истина;
	ОписаниеРеквизита.РедактированиеРазрешено = РедактированиеРазрешено;
	
	СтруктураЭлементов = Новый Структура(БлокируемыеЭлементы);
	Для Каждого КлючЗначение Из СтруктураЭлементов Цикл
		
		Элемент = Элементы.Найти(КлючЗначение.Ключ);
		Если Элемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеРеквизита.БлокируемыеЭлементы.Добавить(КлючЗначение.Ключ);
		Элемент.ТолькоПросмотр = НЕ РедактированиеРазрешено;
		
	КонецЦикла;
	
	ОписаниеРеквизита.Представление = "";
	
	Возврат ЭтотОбъект;
	
КонецФункции

#КонецОбласти

#Область ЭлементыФормы

// Установить параметр элемента.
// 
// Параметры:
//  ИмяПараметра - Строка - Имя параметра
//  Значение - Произвольный - Значение параметра элемента
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция УстановитьПараметрЭлемента(ИмяПараметра, Значение) Экспорт
	
	ПараметрыЭлемента = Новый Структура(ИмяПараметра, Значение);
	УстановитьПараметрыЭлемента(ПараметрыЭлемента);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить параметры элемента.
// 
// Параметры:
//  ПараметрыЭлемента - Структура - Параметры элемента см. УстановитьПараметрЭлемента
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция УстановитьПараметрыЭлемента(ПараметрыЭлемента) Экспорт
	
	ПреобразоватьВСтруктуруЕслиНеопределено(ПараметрыЭлемента);
	ЗаполнитьЗначенияСвойств(ТекущийЭлемент, ПараметрыЭлемента);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить действие элемента.
// 
// Параметры:
//  ИмяСобытия - Строка - Имя параметра
//  Обработчик - Строка - Имя метода-обработчика события
//
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция УстановитьДействиеЭлемента(ИмяСобытия, Обработчик) Экспорт
	
	СобытияЭлемента = Новый Структура(ИмяСобытия, Обработчик);
	УстановитьДействияЭлемента(СобытияЭлемента);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Установить действия элемента.
// 
// Параметры:
//  СобытияЭлемента - Структура - События элемента см. УстановитьДействиеЭлемента
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция УстановитьДействияЭлемента(СобытияЭлемента) Экспорт
	
	ПреобразоватьВСтруктуруЕслиНеопределено(СобытияЭлемента);
	Для Каждого КлючЗначение Из СобытияЭлемента Цикл
		ТекущийЭлемент.УстановитьДействие(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать группу.
// 
// Параметры:
//  Имя - Строка - Имя создаваемой группы
//  Заголовок - Строка - Заголовок
//  Вид - Неопределено, ВидГруппыФормы - Вид
//  Родитель - Неопределено, Строка, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьГруппу(Имя, Заголовок = "", Вид = Неопределено, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПереопределитьРодителя(Родитель);
	Если Вид = Неопределено Тогда
		Вид = ВидГруппыФормы.ОбычнаяГруппа;
	КонецЕсли;
	
	Если ВставитьПередЭлементом = Неопределено Тогда
		Элемент = Элементы.Добавить(Имя, Тип("ГруппаФормы"), Родитель);
	Иначе
		Элемент = Элементы.Вставить(Имя, Тип("ГруппаФормы"), Родитель, ВставитьПередЭлементом);
	КонецЕсли;
	
	Элемент.Вид = Вид;
	Элемент.Заголовок = Заголовок;
	Элемент.ОтображатьЗаголовок = ЗначениеЗаполнено(Заголовок);
	
	УстановитьТекущийЭлемент(Элемент, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать группу без отображения.
// 
// Параметры:
//  Имя - Строка - Имя создаваемой группы
//  Заголовок - Строка - Заголовок
//  Вид - Неопределено, ВидГруппыФормы - Вид
//  Родитель - Неопределено, Строка, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьГруппуБезОтображения(Имя, Заголовок = "", Вид = Неопределено, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	СоздатьГруппу(Имя, Заголовок, Вид, Родитель, ВставитьПередЭлементом);
	ПараметрыЭлемента = НовыйПараметрыГруппыБезОтображения();
	УстановитьПараметрыЭлемента(ПараметрыЭлемента);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать страницы.
// 
// Параметры:
//  Имя - Строка - Имя создаваемой группы
//  Заголовок - Строка - Заголовок
//  Родитель - Неопределено, Строка, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьСтраницы(Имя, Заголовок = "", Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	СоздатьГруппу(Имя, Заголовок, ВидГруппыФормы.Страницы, Родитель, ВставитьПередЭлементом);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать страницу.
// 
// Параметры:
//  Имя - Строка - Имя создаваемой группы
//  Заголовок - Строка - Заголовок
//  Родитель - ГруппаФормы - Родитель. Группа формы с видом "Страницы"
//  ВставитьПередЭлементом - Неопределено, Строка, ГруппаФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьСтраницу(Имя, Заголовок = "", Знач Родитель, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	СоздатьГруппу(Имя, Заголовок, ВидГруппыФормы.Страница, Родитель, ВставитьПередЭлементом);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать декорацию.
// 
// Параметры:
//  Имя - Строка - Имя
//  Заголовок - Строка - Заголовок
//  Вид - Неопределено, ВидДекорацииФормы - Вид
//  Родитель - Неопределено, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьДекорацию(Имя, Заголовок = "", Вид = Неопределено, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПереопределитьРодителя(Родитель);
	Если Вид = Неопределено Тогда
		Вид = ВидДекорацииФормы.Надпись;
	КонецЕсли;
	
	Если ВставитьПередЭлементом = Неопределено Тогда
		Элемент = Элементы.Добавить(Имя, Тип("ДекорацияФормы"), Родитель);
	Иначе
		Элемент = Элементы.Вставить(Имя, Тип("ДекорацияФормы"), Родитель, ВставитьПередЭлементом);
	КонецЕсли;
	
	Элемент.Вид = Вид;
	Элемент.Заголовок = Заголовок;
	
	УстановитьТекущийЭлемент(Элемент, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать поле.
// 
// Параметры:
//  Имя - Строка - Имя
//  Заголовок - Строка - Заголовок
//  ПутьКДанным - Строка - Путь к данным
//  Вид - Неопределено, ВидПоляФормы - Вид
//  Родитель - Неопределено, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьПоле(Имя, Заголовок = "", ПутьКДанным, Вид = Неопределено, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПереопределитьРодителя(Родитель);
	Если Вид = Неопределено Тогда
		Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если ВставитьПередЭлементом = Неопределено Тогда
		Элемент = Элементы.Добавить(Имя, Тип("ПолеФормы"), Родитель);
	Иначе
		Элемент = Элементы.Вставить(Имя, Тип("ПолеФормы"), Родитель, ВставитьПередЭлементом);
	КонецЕсли;
	
	Элемент.ПутьКДанным = ПутьКДанным;
	Элемент.Вид = Вид;
	Элемент.Заголовок = Заголовок;
	
	Если Вид = ВидПоляФормы.ПолеВвода Тогда
		ПараметрыПоУмолчанию = НовыйПараметрыПоляВводаПоУмолчанию();
		
	ИначеЕсли Вид = ВидПоляФормы.ПолеФлажка Тогда
		ПараметрыПоУмолчанию = НовыйПараметрыПоляФлажкаПоУмолчанию();
		
	Иначе
		ПараметрыПоУмолчанию = Новый Структура;
		
	КонецЕсли;
	
	УстановитьПараметрыЭлемента(ПараметрыПоУмолчанию);
	
	УстановитьТекущийЭлемент(Элемент, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать таблицу.
// 
// Параметры:
//  Имя - Строка - Имя
//  Заголовок - Строка - Заголовок
//  ПутьКДанным - Строка - Путь к данным
//  Родитель - Неопределено, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьТаблицу(Имя, Заголовок = "", ПутьКДанным, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПереопределитьРодителя(Родитель);
	
	Если ВставитьПередЭлементом = Неопределено Тогда
		Элемент = Элементы.Добавить(Имя, Тип("ТаблицаФормы"), Родитель);
	Иначе
		Элемент = Элементы.Вставить(Имя, Тип("ТаблицаФормы"), Родитель, ВставитьПередЭлементом);
	КонецЕсли;
	
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элемент.ПутьКДанным = ПутьКДанным;
	Элемент.Заголовок = Заголовок;
	
	УстановитьТекущийЭлемент(Элемент, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать команду.
// 
// Параметры:
//  Имя - Строка - Имя
//  Заголовок - Строка - Заголовок
//  Действие - Строка - Имя метода-обработчика команды
//  ИзменяетСохраняемыеДанные - Булево - Изменяет сохраняемые данные
//  Картинка - Неопределено, Картинка - Картинка
//  Отображение - Неопределено, ОтображениеКнопки - Отображение
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьКоманду(Имя, Заголовок, Действие, ИзменяетСохраняемыеДанные = Истина, Картинка = Неопределено, Отображение = Неопределено) Экспорт
	
	Команда = ПолучитьКоманду(Имя);
	Если Команда <> Неопределено Тогда
		УстановитьТекущуюКоманду(Команда);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	Если Отображение = Неопределено Тогда
		Отображение = ?(Картинка = Неопределено, ОтображениеКнопки.Авто, ОтображениеКнопки.КартинкаИТекст);
	КонецЕсли;
	
	Команда = Команды.Добавить(Имя);
	Команда.Заголовок = Заголовок;
	Команда.Картинка = Картинка;
	Команда.Отображение = Отображение;
	Команда.Действие = Действие;
	Команда.ИзменяетСохраняемыеДанные = ИзменяетСохраняемыеДанные;
	
	УстановитьТекущуюКоманду(Команда, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

// Создать кнопку формы.
// 
// Параметры:
//  Имя - Строка - Имя
//  Заголовок - Строка - Заголовок
//  ИмяКоманды - Строка - Имя команды
//  Вид - Неопределено, ВидКнопкиФормы - Вид
//  Родитель - Неопределено, ЭлементФормы - Родитель
//  ВставитьПередЭлементом - Неопределено, Строка, ЭлементФормы - Вставить перед элементом
// 
// Возвращаемое значение:
//  ЭтотОбъект - Сама обработка
//
Функция СоздатьКнопкуФормы(Имя, Заголовок = "", ИмяКоманды = "", Вид = Неопределено, Знач Родитель = Неопределено, ВставитьПередЭлементом = Неопределено) Экспорт
	
	Элемент = ПолучитьЭлемент(Имя);
	Если Элемент <> Неопределено Тогда
		УстановитьТекущийЭлемент(Элемент);
		Возврат ЭтотОбъект;
	КонецЕсли;
	
	ПереопределитьРодителя(Родитель);
	Если Вид = Неопределено Тогда
		Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяКоманды) Тогда
		ИмяКоманды = Имя;
		СоздатьКоманду(ИмяКоманды, Заголовок, ИмяКоманды);
	КонецЕсли;
	
	Если ВставитьПередЭлементом = Неопределено Тогда
		Элемент = Элементы.Добавить(Имя, Тип("КнопкаФормы"), Родитель);
	Иначе
		Элемент = Элементы.Вставить(Имя, Тип("КнопкаФормы"), Родитель, ВставитьПередЭлементом);
	КонецЕсли;
	
	Элемент.Вид = Вид;
	Элемент.Заголовок = Заголовок;
	Элемент.ИмяКоманды = ИмяКоманды;
	
	УстановитьТекущийЭлемент(Элемент, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

#КонецОбласти

#Область УсловноеОформление

// TODO: Добавить управление условным оформлением

#КонецОбласти

#КонецОбласти

#Область КонструкторыТиповыхПараметровЭлементов

// Возвращает основные параметры поля ввода согласно стандартов
// 
// Возвращаемое значение:
//  Структура - Новый параметры поля ввода по умолчанию85:
// * ПоложениеЗаголовка - ПоложениеЗаголовкаЭлементаФормы - 
// * АвтоМаксимальнаяШирина - Булево - 
// * МаксимальнаяШирина - Число - 
//
Функция НовыйПараметрыПоляВводаПоУмолчанию() Экспорт
	
	ПараметрыПоляВвода = Новый Структура;
	ПараметрыПоляВвода.Вставить("ПоложениеЗаголовка",     ПоложениеЗаголовкаЭлементаФормы.Лево);
	ПараметрыПоляВвода.Вставить("АвтоМаксимальнаяШирина", Ложь);
	ПараметрыПоляВвода.Вставить("МаксимальнаяШирина",     28);
	
	Возврат ПараметрыПоляВвода;
	
КонецФункции

// Возвращает основные параметры поля ввода согласно стандартов для платформы 8.5
// 
// Возвращаемое значение:
//  Структура - Новый параметры поля ввода по умолчанию85:
// * ПоложениеЗаголовка - ПоложениеЗаголовкаЭлементаФормы - 
// * АвтоМаксимальнаяШирина - Булево - 
// * МаксимальнаяШирина - Число - 
// 
Функция НовыйПараметрыПоляВводаПоУмолчанию85() Экспорт
	
	ПараметрыПоляВвода = Новый Структура;
	ПараметрыПоляВвода.Вставить("ПоложениеЗаголовка",     ПоложениеЗаголовкаЭлементаФормы.Авто);
	ПараметрыПоляВвода.Вставить("АвтоМаксимальнаяШирина", Ложь);
	ПараметрыПоляВвода.Вставить("МаксимальнаяШирина",     28);
	
	Возврат ПараметрыПоляВвода;
	
КонецФункции


// Возвращает основные параметры поля флажка согласно стандартов
// 
// Параметры:
//  Вид - ВидФлажка - Вид флажка. По умолчанию - Авто
// 
// Возвращаемое значение:
//  Структура - Новый параметры поля флажка по умолчанию:
// * ПоложениеЗаголовка - ПоложениеЗаголовкаЭлементаФормы - 
// * АвтоМаксимальнаяШирина - Булево - 
// * МаксимальнаяШирина - Число - 
// * ВидФлажка - ВидФлажка - 
// 
Функция НовыйПараметрыПоляФлажкаПоУмолчанию(Знач Вид = Неопределено) Экспорт
	
	Если Вид = Неопределено Тогда
		Вид = ВидФлажка.Авто;
	КонецЕсли;
	
	ПараметрыПоля = Новый Структура;
	ПараметрыПоля.Вставить("ПоложениеЗаголовка",     ПоложениеЗаголовкаЭлементаФормы.Право);
	ПараметрыПоля.Вставить("АвтоМаксимальнаяШирина", Ложь);
	ПараметрыПоля.Вставить("МаксимальнаяШирина",     28);
	ПараметрыПоля.Вставить("ВидФлажка",              Вид);
	
	Возврат ПараметрыПоля;
	
КонецФункции

// Параметры группы без отображения.
// 
// Возвращаемое значение:
//  Структура - Новый параметры группы без отображения:
// * ОтображатьЗаголовок - Булево - 
// * Объединенная - Булево - 
// * Отображение - ОтображениеОбычнойГруппы - 
// 
Функция НовыйПараметрыГруппыБезОтображения() Экспорт
	
	ПараметрыГруппы = Новый Структура;
	ПараметрыГруппы.Вставить("ОтображатьЗаголовок", Ложь);
	ПараметрыГруппы.Вставить("Объединенная",        Истина);
	ПараметрыГруппы.Вставить("Отображение",         ОтображениеОбычнойГруппы.Нет);
	
	Возврат ПараметрыГруппы;
	
КонецФункции

#КонецОбласти

#Область ПолучениеДанныхФомрмы

// Возвращает последний созданный элемент или находит в коллекции элементов формы
//
// Параметры:
//  Имя - Строка - Имя элемента
// 
// Возвращаемое значение:
//  Элемент управления, Неопределено - Элемент формы или Неопределно, если не найден по имени
//
Функция ПолучитьЭлемент(Имя = "") Экспорт
	
	Если ПустаяСтрока(Имя) Тогда
		Результат = ТекущийЭлемент;
	Иначе
		Результат = КешЭлементов[Имя];
		Если Результат = Неопределено Тогда
			Результат = Элементы.Найти(Имя);
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает последнюю созданную команду или находит в коллекции элементов формы
//
// Параметры:
//  Имя - Строка - Имя команды
// 
// Возвращаемое значение:
//  Команда, Неопределено - Команда формы или Неопределно, если не найдена по имени
//
Функция ПолучитьКоманду(Имя = "") Экспорт
	
	Если ПустаяСтрока(Имя) Тогда
		Результат = ТекущаяКоманда;
	Иначе
		Результат = КешКоманд[Имя];
		Если Результат = Неопределено Тогда
			Результат = Команды.Найти(Имя);
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Проверки

// Проверка на запрет доработки формы.
// Используется для отключения создания новых элементов на время тестирования
// 
// Возвращаемое значение:
//  Булево - Запрет доработки формы
// 
Функция ЗапретДоработкиФормы() Экспорт
	
	ЗапретДоработки = Неопределено;
	Параметры.Свойство(ИмяПараметраЗапретДоработкиФормы, ЗапретДоработки);
	Возврат (ЗапретДоработки = Истина);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПреобразоватьВСтруктуруЕслиНеопределено(ВходящиеДанные)
	Если ВходящиеДанные = Неопределено Тогда
		ВходящиеДанные = Новый Структура;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрекфиксКонструктора()
	Возврат "zdc";
КонецФункции

Функция ЕстьРеквизитИлиСвойствоОбъекта(ПроверяемыйОбъект, ИмяРеквизита)
	
	КлючУникальности   = Новый УникальныйИдентификатор;
	СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
	ЗаполнитьЗначенияСвойств(СтруктураРеквизита, ПроверяемыйОбъект);
	
	Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;
	
КонецФункции

Процедура УстановитьТекущийЭлемент(Элемент, Кешировать = Ложь)
	
	ТекущийЭлемент = Элемент;
	Если НЕ Кешировать Тогда
		Возврат;
	КонецЕсли;
	
	КешЭлементов.Вставить(Элемент.Имя, Элемент);
	
КонецПроцедуры

Процедура УстановитьТекущуюКоманду(Команда, Кешировать = Ложь)
	
	ТекущаяКоманда = Команда;
	Если НЕ Кешировать Тогда
		Возврат;
	КонецЕсли;
	
	КешКоманд.Вставить(Команда.Имя, Команда);
	
КонецПроцедуры

Процедура ПереопределитьРодителя(Родитель)
	
	Если ТипЗнч(Родитель) = Тип("Строка") Тогда
		Родитель = ПолучитьЭлемент(Родитель);
	КонецЕсли;
	
	// Не через ИначеЕсли, т.к. Элемент может быть не найден
	Если Родитель = Неопределено Тогда
		Родитель = Форма;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗначениеВМассиве(Знач Значение) Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Значение);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Инициализация

ТекущийЭлемент = Неопределено;
ТекущаяКоманда = Неопределено;
Интерфейс85 = Ложь;
КешЭлементов = Новый Соответствие;
КешКоманд = Новый Соответствие;

УсловноеОформлениеЭлемент = Неопределено;
УсловноеОформлениеКеш = Новый Соответствие;

РеквизитыФормы = Новый Соответствие;
ОсновнойРеквизитОписание = Новый Структура;
ОсновнойРеквизитОписание.Вставить("Имя", "");
ОсновнойРеквизитОписание.Вставить("Реквизит");
СозданныеРеквизиты = Новый Соответствие;
ДобавляемыеРеквизиты = Новый Массив;
УдаляемыеРеквизиты = Новый Массив;

ИмяПараметраЗапретДоработкиФормы = ПрекфиксКонструктора() + "ЗапретРедактирования";

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
