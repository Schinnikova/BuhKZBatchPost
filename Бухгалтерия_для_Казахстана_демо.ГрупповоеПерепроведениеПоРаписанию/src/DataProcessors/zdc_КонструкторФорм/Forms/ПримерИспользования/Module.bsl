
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдаптироватьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТестПриИзменении(Элемент)
	Сообщить(ЭтотОбъект["Тест"]);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработчикКоманды1(Команда)
	
	Сообщить(Команда.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АдаптироватьФорму()
	
	Конструктор = КонструкторФорм(ЭтотОбъект);
	Если Конструктор.ЗапретДоработкиФормы() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоляВвода = Конструктор.НовыйПараметрыПоляВводаПоУмолчанию();
	ПараметрыПоляВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;

	ИмяТаблицы = "ТестоваяТаблица";
	Конструктор.СоздатьРеквизит("Тест", "Строка")
		.СоздатьРеквизит("Тест2", "Строка")
		.СоздатьРеквизит(ИмяТаблицы, "ТаблицаЗначений")
		.СоздатьРеквизит("КолонкаБулево1", "Булево", ИмяТаблицы)
		.СоздатьРеквизит("КолонкаБулево2", "Булево", ИмяТаблицы)
		.СоздатьРеквизит("КолонкаСтрока1", "Строка", ИмяТаблицы)
		.СоздатьРеквизит("КолонкаСтрока2", "Строка", ИмяТаблицы)
		.ИзменитьРеквизиты();
		
	Конструктор
		.СоздатьГруппу("ТестоваяГруппа", "Заголовок группы")
		.УстановитьПараметрЭлемента("Поведение", ПоведениеОбычнойГруппы.Всплывающая)
		.СоздатьПоле("ПолеРеквизитаТест",, "Тест",, "ТестоваяГруппа")
		.УстановитьДействиеЭлемента("ПриИзменении", "ТестПриИзменении");
	
	Конструктор.СоздатьТаблицу(ИмяТаблицы, , ИмяТаблицы)
		.СоздатьПоле(ИмяТаблицы + "КолонкаБулево1", , ИмяТаблицы + ".КолонкаБулево1", ВидПоляФормы.ПолеФлажка, ИмяТаблицы)
		.СоздатьПоле(ИмяТаблицы + "КолонкаБулево2", , ИмяТаблицы + ".КолонкаБулево2", ВидПоляФормы.ПолеФлажка, ИмяТаблицы)
		.СоздатьПоле(ИмяТаблицы + "КолонкаСтрока1", , ИмяТаблицы + ".КолонкаСтрока1", , ИмяТаблицы)
		.СоздатьПоле(ИмяТаблицы + "КолонкаСтрока2", , ИмяТаблицы + ".КолонкаСтрока2", , ИмяТаблицы)
		
		.СоздатьДекорацию("ОбычнаяДекорация", "Супер-декорация")
		.СоздатьПоле("ПолеРеквизитаТест2",, "Тест2")
		.УстановитьПараметрыЭлемента(ПараметрыПоляВвода);
		
	Конструктор.СоздатьКоманду("ТестоваяКоманда", "Дер-кнопка", "ОбработчикКоманды1",, БиблиотекаКартинок.Обновить)
		.СоздатьКнопкуФормы("ТестоваяКоманда", , "ТестоваяКоманда",, "ТестоваяГруппа");
		
КонецПроцедуры

&НаСервере
Функция КонструкторФорм(Форма)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Инициализировать(Форма);
	
	Возврат ОбработкаОбъект;
	
КонецФункции

#КонецОбласти

